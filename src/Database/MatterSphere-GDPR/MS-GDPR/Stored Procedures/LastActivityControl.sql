CREATE PROCEDURE [dbo].[LastActivityControl]
AS
DECLARE @SQL nvarchar(max)
DECLARE @LCV bigint,@WV bigint
DECLARE @SCHEMA nvarchar(125),@TBL nvarchar(125)

BEGIN
	SELECT 
		 @LCV=LastCheckedVersion,
		 @WV=WorkingVersion
	FROM config.ChangeVersionControl 

	SELECT @WV=CHANGE_TRACKING_CURRENT_VERSION( );

	DECLARE WORK_CURSOR CURSOR FOR

	WITH Controller_CTE (TABLE_SCHEMA,TABLE_NAME)
	AS
	(
	SELECT  ST.TABLE_SCHEMA,ST.TABLE_NAME
	  FROM [dbo].[PII_Tables] T  JOIN INFORMATION_SCHEMA.TABLES ST ON  ST.TABLE_NAME = T.PII_TableName AND TABLE_TYPE ='BASE TABLE'
	  Where PII_IsController = 1
	  )
	SELECT 'Update '+QUOTENAME(CTE.TABLE_SCHEMA)+'.'+QUOTENAME(CTE.TABLE_NAME)+
	' SET LAST_ACTIVITY = getdate() '+
	' FROM '+QUOTENAME(ST.Table_SCHEMA)+'.'+QUOTENAME(J.TO_Table_Name)+' AS [B] '+
	' JOIN '+QUOTENAME(CTE.TABLE_SCHEMA)+'.'+QUOTENAME(CTE.TABLE_NAME)+ 'AS [C] ON [B].'+QUOTENAME(J.TO_Join_column)+' = [C].'+QUOTENAME(J.FR_Join_column) +
	' JOIN  CHANGETABLE( CHANGES '+QUOTENAME(ST.Table_SCHEMA)+'.'+QUOTENAME(J.TO_Table_Name)+','+CONVERT(nvarchar(10),isnull(@LCV,0))+') AS [CT] ON ',
	ST.Table_SCHEMA,
	J.TO_Table_Name
	FROM Controller_CTE CTE
	  JOIN [dbo].[PII_Tables] T on CTE.TABLE_NAME = T.PII_TableName
	  JOIN  [dbo].[PII_Table_Join] J on T.Id = J.FR_Table_Name
	  JOIN INFORMATION_SCHEMA.TABLES ST ON  ST.TABLE_NAME = J.TO_Table_Name AND TABLE_TYPE ='BASE TABLE'
	  Where ActivityToController = 1 
	  AND J.TO_Table_Name <> CTE.TABLE_NAME
 
	 OPEN WORK_CURSOR 
	 FETCH NEXT FROM WORK_CURSOR into @SQL,@SCHEMA,@TBL
	 WHILE @@FETCH_STATUS = 0
	  BEGIN
		DECLARE @KEYMATCH nvarchar(max)
		set @KEYMATCH = null
		select  @KEYMATCH= COALESCE(@KEYMATCH+' AND ','')+'[CT].'+COLUMN_NAME+' =  [B].'+COLUMN_NAME from INFORMATION_SCHEMA.KEY_COLUMN_USAGE 
			WHERE OBJECTPROPERTY(OBJECT_ID(CONSTRAINT_SCHEMA + '.' + QUOTENAME(CONSTRAINT_NAME)), 'IsPrimaryKey') = 1
			AND TABLE_SCHEMA = @SCHEMA AND TABLE_NAME = @TBL
		print @SQL+@KEYMATCH
		exec (@SQL+@KEYMATCH)
		FETCH NEXT FROM WORK_CURSOR into @SQL,@SCHEMA,@TBL
	 END
	 CLOSE WORK_CURSOR
	 DEALLOCATE WORK_CURSOR

 	UPDATE config.ChangeVersionControl
	SET LastCheckedVersion = @WV;
 
 END
RETURN 0
