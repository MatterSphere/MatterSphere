CREATE PROCEDURE dbo.SCHCOMUNDTAKING
(
	@UI uUICultureInfo = '{default}'
	, @UTAKINGTYPE uCodeLookup = NULL
	, @LOGGEDDATESTART DATETIME = NULL
	, @LOGGEDDATEFIN DATETIME = NULL
	, @SHOWCOMPLETED INT = 0
	, @AUTHBY INT = NULL
	, @KEYWORD NVARCHAR(MAX) = NULL
	, @DEPT uCodeLookup = NULL
	, @ORDERBY NVARCHAR(MAX) = NULL
	, @MAX_RECORDS INT = 50
	, @ACTIVE BIT = NULL
	, @PageNo INT = 1
)  
AS
SET TRAN ISOLATION LEVEL READ UNCOMMITTED
SET NOCOUNT ON

DECLARE @SELECT NVARCHAR(MAX)

SET @SELECT = N'
DECLARE @OFFSET INT = 0,
	@TOTAL int  = 0, @TOP INT
	
IF @MAX_RECORDS > 0
	SET @TOP = @MAX_RECORDS
ELSE
	SET @TOP = 50

IF @PageNo IS NULL
	SET @OFFSET = 0
ELSE
	SET @OFFSET = @TOP * (@PageNo- 1)


DECLARE @Res TABLE(
	Id BIGINT PRIMARY KEY
	, undID INT
);

WITH Res AS
(
SELECT 
	CASE WHEN UND.FILEID IS NOT NULL THEN DBO.GETFILEREF(C.CLNO, F.FILENO) ELSE CLNO END AS FILEREF
	, C.CLNAME
	, UND.*
	, COALESCE(CL1.cdDesc, ''~'' + NULLIF(UND.undType, '''') + ''~'') AS UndTypeDesc
	, U.usrInits as AuthByInits
	, U.usrFullName as AuthByFullName
	, COALESCE(CL2.cdDesc, ''~'' + NULLIF(F.fileDepartment, '''') + ''~'') AS Department
	, COALESCE(CL3.cdDesc, ''~'' + NULLIF(F.fileType, '''') + ''~'') AS FileTypeDesc
FROM dbo.dbundertakings UND
	INNER JOIN dbClient C ON C.CLID = UND.CLID
	INNER JOIN dbo.dbUser U ON U.USRID = UND.UNDAUTHBY
	INNER JOIN dbFile F ON UND.FILEID = F.FILEID
	LEFT OUTER JOIN dbo.GetCodeLookupDescription (''UNDERTYPE'', @UI) CL1 ON CL1.cdCode = UND.undType
	LEFT OUTER JOIN dbo.GetCodeLookupDescription (''DEPT'', @UI) CL2 ON CL2.cdCode = F.fileDepartment
	LEFT OUTER JOIN dbo.GetCodeLookupDescription (''FILETYPE'', @UI) CL3 ON CL3.cdCode = F.fileType
WHERE 1 = 1
	'
IF @UTAKINGTYPE IS NOT NULL
	SET @SELECT = @SELECT + N'AND UND.undType = @UTAKINGTYPE
	'

IF @LOGGEDDATESTART IS NOT NULL
	SET @SELECT = @SELECT + N'AND UND.Created >= @LOGGEDDATESTART
	'

IF @LOGGEDDATEFIN IS NOT NULL
	SET @SELECT = @SELECT + N'AND UND.Created <= @LOGGEDDATEFIN
	'

IF @SHOWCOMPLETED = 0
	SET @SELECT = @SELECT + N'AND UND.UNDCOMPLETED IS NULL
	'
IF @SHOWCOMPLETED = 1
	SET @SELECT = @SELECT + N'AND UND.UNDCOMPLETED IS NOT NULL
	'
IF @KEYWORD IS NOT NULL
	SET @SELECT = @SELECT + N'AND UND.undDesc LIKE ''%'' + COALESCE(@KEYWORD, UND.undDesc) + ''%''
	'
IF @AUTHBY IS NOT NULL
	SET @SELECT = @SELECT + N'AND UND.undAuthBy = @AUTHBY
	'
IF @ACTIVE IS NOT NULL
	SET @SELECT = @SELECT + N'AND UND.undActive = @ACTIVE
	'
IF @DEPT IS NOT NULL
	SET @SELECT = @SELECT + N'AND F.FileDepartment = @DEPT
	'

SET @SELECT = @SELECT + N'
)

INSERT INTO @Res (undID, Id)
SELECT 
	undID
	'
IF @ORDERBY IS NULL
	SET @SELECT = @SELECT + N', ROW_NUMBER() OVER(ORDER BY FILEREF)'
ELSE 
	IF @ORDERBY NOT LIKE '%FILEREF%'
		SET @SELECT = @SELECT + N', ROW_NUMBER() OVER(ORDER BY ' + @ORDERBY  + N', FILEREF)'
	ELSE 
		SET @SELECT = @SELECT + N', ROW_NUMBER() OVER(ORDER BY ' + @ORDERBY  + N')'

SET @SELECT = @SELECT + N'
FROM Res

SET @Total = @@ROWCOUNT

SELECT TOP(@TOP)
	CASE WHEN UND.FILEID IS NOT NULL THEN DBO.GETFILEREF(C.CLNO, F.FILENO) ELSE CLNO END AS FILEREF
	, C.CLNAME
	, UND.*
	, COALESCE(CL1.cdDesc, ''~'' + NULLIF(UND.undType, '''') + ''~'') AS UndTypeDesc
	, U.usrInits as AuthByInits
	, U.usrFullName as AuthByFullName
	, COALESCE(CL2.cdDesc, ''~'' + NULLIF(F.fileDepartment, '''') + ''~'') AS Department
	, COALESCE(CL3.cdDesc, ''~'' + NULLIF(F.fileType, '''') + ''~'') AS FileTypeDesc
	, @TOTAL as Total
FROM @RES res
	INNER JOIN dbo.dbundertakings UND ON UND.undID = res.undID
	INNER JOIN dbClient C ON C.CLID = UND.CLID
	INNER JOIN dbo.dbUser U ON U.USRID = UND.UNDAUTHBY
	INNER JOIN dbFile F ON UND.FILEID = F.FILEID
	LEFT OUTER JOIN dbo.GetCodeLookupDescription (''UNDERTYPE'', @UI) CL1 ON CL1.cdCode = UND.undType
	LEFT OUTER JOIN dbo.GetCodeLookupDescription (''DEPT'', @UI) CL2 ON CL2.cdCode = F.fileDepartment
	LEFT OUTER JOIN dbo.GetCodeLookupDescription (''FILETYPE'', @UI) CL3 ON CL3.cdCode = F.fileType
WHERE res.Id > @OFFSET 
ORDER BY res.Id
'
PRINT @SELECT
EXEC sp_executesql @SELECT, N'@UI uUICultureInfo, @UTAKINGTYPE uCodeLookup, @LOGGEDDATESTART DATETIME, @LOGGEDDATEFIN DATETIME, @SHOWCOMPLETED INT, @AUTHBY INT, @KEYWORD NVARCHAR(MAX), @DEPT uCodeLookup, @MAX_RECORDS INT, @ACTIVE BIT, @PageNo INT', 
	@UI, @UTAKINGTYPE, @LOGGEDDATESTART, @LOGGEDDATEFIN, @SHOWCOMPLETED, @AUTHBY, @KEYWORD, @DEPT, @MAX_RECORDS, @ACTIVE, @PageNo

GO

GRANT EXECUTE
    ON OBJECT::[dbo].[SCHCOMUNDTAKING] TO [OMSRole]
    AS [dbo];


GO
GRANT EXECUTE
    ON OBJECT::[dbo].[SCHCOMUNDTAKING] TO [OMSAdminRole]
    AS [dbo];